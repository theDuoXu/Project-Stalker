import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer
import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin'
    // Aplicamos Spring Boot para gestión de dependencias y DI, pero NO generamos un fat-jar ejecutable de servidor
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'

    // Empaquetador de fat jars para el desktop
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Configuración de la aplicación principal
application {
    mainClass = 'projectstalker.ui.StalkerUiLauncher'
}

javafx {
    version = "21.0.5"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing', 'javafx.web']
}

dependencies {
    // --- CAPA DE DOMINIO (DTOs compartidos) ---
    implementation project(':core-domain')

    // --- SPRING BOOT INTEGRATION ---
    // Usamos 'starter' para el contexto DI y 'webflux' para el cliente HTTP reactivo (High Performance)
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // --- WEBSOCKET & REAL-TIME ---
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.integration:spring-integration-stomp'
    implementation 'org.springframework.integration:spring-integration-websocket'


    // --- UI UX & THEMING (Look & Feel Profesional) ---
    // AtlantaFX: Tema moderno (Nord/Primer) - Estandariza CSS
    implementation 'io.github.mkpaz:atlantafx-base:2.1.0'
    // ControlsFX: Componentes extra (Notificaciones, Breadcrumbs, Toggles)
    implementation 'org.controlsfx:controlsfx:11.2.1'
    // Ikonli: Iconos vectoriales (Material Design)
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'

    // --- UTILIDADES ---
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Logging UI
    implementation 'ch.qos.logback:logback-classic'
}

// Deshabilitar la generación del jar de Spring Boot (es una app de escritorio, no un servidor)
bootJar {
    enabled = false
}

// Asegurar que corre como aplicación Java estándar
jar {
    enabled = true
    manifest {
        attributes(
                'Main-Class': 'projectstalker.ui.StalkerUiLauncher'
        )
    }
}

tasks.register('integrationTest', Test) {
    description = 'Ejecuta tests de integración de UI (Login, Flujos).'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    // CRÍTICO: Por defecto Gradle intenta correr tests sin pantalla (headless).
    // GitHub Actions siempre define la variable de entorno "CI=true".
    // Solo forzamos modo gráfico si NO estamos en CI.
    if (!System.getenv().containsKey('CI')) {
        systemProperty 'java.awt.headless', 'false'
    }

    useJUnitPlatform {
        includeTags 'Integration'
    }

    // Para ver los logs "System.out.println" en la consola mientras corre
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        showExceptions true
        showStackTraces true
    }
}

tasks.named('shadowJar') {
    archiveBaseName.set('ProjectStalkerDSS')
    archiveClassifier.set('standalone')
    archiveVersion.set('')

    // Usamos las rutas completas para evitar el error "Unknown property"

    // 1. Fusionar archivos de configuración de Spring
    transform(ServiceFileTransformer)

    transform(AppendingTransformer) {
        resource = 'META-INF/spring.handlers'
    }
    transform(AppendingTransformer) {
        resource = 'META-INF/spring.schemas'
    }
    transform(AppendingTransformer) {
        resource = 'META-INF/spring.tooling'
    }

    // 2. Incluir application.properties y otros recursos
    from sourceSets.main.output

    // 3. Definir el Main-Class
    manifest {
        attributes(
                'Main-Class': 'projectstalker.ui.StalkerUiLauncher',
                'Multi-Release': 'true'
        )
    }
}

// Build ahora ejecuta shadowJar
//tasks.named('build') {
//    dependsOn tasks.named('shadowJar')
//}  // deshabilitado temporalmente (en prod habilitar)