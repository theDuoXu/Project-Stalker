import projectstalker.build.CompileCudaTask

plugins {
    id 'cpp-library'
}
def cudaHome = '/usr/local/cuda'

library {
    publicHeaders.from('src/main/cpp/include')
    baseName = 'manning_solver'
}

// Librerías para JNI y CUDA
tasks.withType(CppCompile) {
    def javaHome = System.getenv("JAVA_HOME")
    if (javaHome) {
        compilerArgs.add "-I${javaHome}/include"
        compilerArgs.add "-I${javaHome}/include/linux"
    } else {
        logger.warn("JAVA_HOME is not set. C++ compilation might fail to find JNI headers.")
        logger.warn("JAVA_HOME is set to default /usr/lib/jvm/java-21-openjdk-amd64")
        javaHome = "/usr/lib/jvm/java-21-openjdk-amd64"
        compilerArgs.add "-I${javaHome}/include"
        compilerArgs.add "-I${javaHome}/include/linux"

    }
// info de debug en el código C++ host DEBE ELIMINARSE EN PRODUCCIÓN
    compilerArgs.add "-g"
    compilerArgs.add "-O2"

    compilerArgs.add "-I${cudaHome}/include"
}

// 1. REGISTRAR NUESTRA TAREA DE CUDA
def compileCuda = tasks.register('compileCuda', CompileCudaTask) {
    description = 'Compila los archivos .cu en archivos objeto .o'
    group = 'build'
    sourceDir.set(project.file('src/main/cpp/src'))
    // IMPORTANTE: Ponemos los .o en un directorio único
    outputDir.set(project.layout.buildDirectory.dir('obj/cuda'))
}

// 2. CONECTAR
// Configuramos las dependencias de enlace para *ambos* binarios (debug y release)
library.binaries.configureEach { binary ->
    // Obtenemos la tarea de enlace específica (ej. 'linkDebug')
    def linkTask = binary.linkTask.get()

    // Esto incluirá manning_kernel.o, transport_kernel.o y cualquier otro futuro.
    linkTask.linkerArgs.addAll(compileCuda.map { task ->
        // Obtenemos el directorio de salida
        File objDir = task.outputDir.get().asFile

        // Si el directorio existe, listamos los .o. Si no (ej. durante clean), devolvemos lista vacía.
        if (objDir.exists()) {
            return objDir.listFiles(new FilenameFilter() {
                @Override
                boolean accept(File dir, String name) {
                    return name.endsWith(".o")
                }
            }).collect { it.absolutePath }
        }
        return []
    })
    // --- FIN DE LA CONEXIÓN ---

    // Añadimos las librerías de runtime de CUDA
    linkTask.linkerArgs.add("-L${cudaHome}/lib64")
    linkTask.linkerArgs.add('-lcudart')

    // Mantenemos la dependencia explícita para asegurar el orden: Compilar CUDA -> Enlazar C++
    linkTask.dependsOn(compileCuda)
}